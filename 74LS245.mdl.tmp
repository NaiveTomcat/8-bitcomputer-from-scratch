**********************
* Autodesk EAGLE - SPICE Model File
* Author:
* Date: 
**********************
.SUBCKT 74LS245  DIR_I GBAR_I
+ A1_B A2_B A3_B A4_B A5_B A6_B A7_B A8_B
+ B1_B B2_B B3_B B4_B B5_B B6_B B7_B B8_B
+ OPTIONAL: DPWR=$G_DPWR DGND=$G_DGND
+ PARAMS: MNTYMXDLY=0 IO_LEVEL=0
*
U1 BUFA(2) DPWR DGND
+ DIR_I GBAR_I
+ DIR   GBAR
+ D0_GATE IO_LS IO_LEVEL={IO_LEVEL}
U2 INV DPWR DGND
+ DIR   DIRBAR
+ D0_GATE IO_LS
U3 NORA(2,2) DPWR DGND
+ DIRBAR GBAR DIR GBAR
+ ENABLEAB    ENABLEBA
+ D0_GATE IO_LS
*
U4 BUF3A(8) DPWR DGND
+ A1_B A2_B A3_B A4_B A5_B A6_B A7_B A8_B
+ ENABLEAB
+ B1_B B2_B B3_B B4_B B5_B B6_B B7_B B8_B
+ D_LS245 IO_LS_ST
+ MNTYMXDLY={MNTYMXDLY} IO_LEVEL={IO_LEVEL}
U5 BUF3A(8) DPWR DGND
+ B1_B B2_B B3_B B4_B B5_B B6_B B7_B B8_B
+ ENABLEBA
+ A1_B A2_B A3_B A4_B A5_B A6_B A7_B A8_B
+ D_LS245 IO_LS_ST
+ MNTYMXDLY={MNTYMXDLY} IO_LEVEL={IO_LEVEL}
*
.MODEL D_LS245 UTGATE (
+ TPLHTY= 8NS  TPLHMX=12NS
+ TPHLTY= 8NS  TPHLMX=12NS
+ TPZHTY=25NS  TPZHMX=40NS
+ TPZLTY=27NS  TPZLMX=40NS
+ TPHZTY=15NS  TPHZMX=28NS
+ TPLZTY=15NS  TPLZMX=25NS
+ )
*
.ENDS